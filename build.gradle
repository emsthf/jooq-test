buildscript {
    ext {
        jooqVersion = '3.19.8'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.0'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'nu.studer.jooq' version '9.0'
}

group = 'com.sight'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    // jOOQ 의존성 수동설정
    implementation('org.springframework.boot:spring-boot-starter-jooq') {
        exclude group: 'org.jooq:jooq'
    }

    implementation "org.jooq:jooq:${jooqVersion}"
    jooqGenerator project(':jOOQ-custom')
    // codegen 시점에 entity 모듈 포함
    jooqGenerator project(':entity')
    jooqGenerator "org.jooq:jooq:${jooqVersion}"
    jooqGenerator "org.jooq:jooq-meta:${jooqVersion}"
    jooqGenerator "org.jooq:jooq-meta-extensions-hibernate:${jooqVersion}"
    // in-memory H2로 JOOQ dsl 생성을 위함
    // h2의 v2 버전 호환성 때문에 상위버전 대신 1.4.200 버전을 추천
    jooqGenerator 'com.h2database:h2:1.4.200'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'com.mysql:mysql-connector-j'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

String dbUser   = System.properties["db-user"]   ?: "root"
String dbPasswd = System.properties["db-passwd"] ?: "passwd"

jooq {
    version = "${jooqVersion}"
    configurations {
        sakilaDb {
            generationTool {

                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.extensions.jpa.JPADatabase'
                        unsignedTypes = true
                        properties {
                            property {
                                key = 'packages'
                                value = 'com.sight.entity'
                            }

                            // DSL 생성시, jpa의 AttributeConverters 에 따라 jooq DSL의 타입을 매핑할지 여부
                            property {
                                key = 'useAttributeConverters'
                                value = true
                            }
                        }

                        // 강제로 타입을 매핑할 수 있음
                        forcedTypes {
                            forcedType {
                                userType = 'java.lang.Long'
                                includeTypes = 'int unsigned'
                            }

                            forcedType {
                                userType = 'java.lang.Integer'
                                includeTypes = 'tinyint unsigned'
                            }

                            forcedType {
                                userType = 'java.lang.Integer'
                                includeTypes = 'smallint unsigned'
                            }
                        }
                    }

                    generate {
                        daos = true
                        records = true
                        fluentSetters = true
                        javaTimeTypes = true
                        deprecated = false

                        jpaAnnotations = true
                        jpaVersion = 2.2
//                        validationAnnotations = true  // 컬럼의 조건에 맞는 validation 어노테이션을 붙여줌
//                        springAnnotations = true
//                        springDao = true
                    }

                    target {
                        directory = 'src/generated'
                    }

                    // 왜 따로 서브모듈로 분리를 한걸까?
                    strategy.name = 'jooq.custom.generator.JPrefixGeneratorStrategy'
                }
            }
        }
    }

    sourceSets {
        main {
            java {
                srcDirs = ["src/main/java", "src/generated"]
            }
        }
    }
}
